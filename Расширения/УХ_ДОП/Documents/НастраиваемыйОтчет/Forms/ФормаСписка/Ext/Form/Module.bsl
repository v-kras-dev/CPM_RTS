#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы
&НаКлиенте
Процедура Расш1_ПриОткрытииПосле(Отказ)
	Расш1_ПриОткрытииПослеНаСервере();
КонецПроцедуры
&НаСервере
Процедура Расш1_ПриОткрытииПослеНаСервере()
	Если ОбщегоПользованияУХ.ЕстьРоль("ВыгрузкаCводныхОтчетовНаДиск",Пользователи.ТекущийПользователь()) Тогда
		НоваяКоманда = ЭтаФорма.Команды.Добавить("СохранитьНаДиск");
		НоваяКоманда.Действие = "ВыполнитьКоманду";
		НоваяКоманда.Заголовок = "Сохранить на диск";
		
		НоваяКнопка = ЭтаФорма.Элементы.Вставить("КнопкаСохранитьНаДиск",Тип("КнопкаФормы"),ЭтаФорма.Элементы.ВидОтчетаСодержание);
		НоваяКнопка.ИмяКоманды = "СохранитьНаДиск";
		НоваяКнопка.Вид = ВидКнопкиФормы.Гиперссылка;  
	КонецЕсли;  
КонецПроцедуры   
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ВыполнитьКоманду(Команда) 
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеВыбораКаталога",ЭтотОбъект);	
	ОткрытьФорму("ОбщаяФорма.Расш1_ФормаВыбораФайла",,ЭтотОбъект,,,,ОповещениеОЗакрытии);
КонецПроцедуры    
&НаКлиенте
Процедура ПослеВыбораКаталога(Результат, Параметры) Экспорт
	Если Результат <> Неопределено Тогда   
		Если Результат.Свойство("Каталог") Тогда
			МассивОтчетов = ОтобратьДляВыгрузкиНаСервере();
			Для Каждого Эл Из МассивОтчетов Цикл 
				Ключ = Новый Структура("Ключ", Эл.Ссылка);
				Форма = ПолучитьФорму("Документ.НастраиваемыйОтчет.Форма.ФормаДокумента", Ключ);
				ТабДок = Форма.ПолеТабличногоДокументаМакет;
				Попытка
					ТабДок.Записать(Результат.Каталог+"\"+Эл.Наименование+".xlsx", ТипФайлаТабличногоДокумента.XLSX);    
					ОбщегоНазначенияКлиент.СообщитьПользователю("Сохранен отчет "+Результат.Каталог+"\"+Эл.Наименование+".");
				Исключение    
					ОбщегоНазначенияКлиент.СообщитьПользователю("Произошла ошибка при сохранении отчета - "+Результат.Каталог+"\"+Эл.Наименование+". Обратитесь к администраторам системы.");
				КонецПопытки;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервереБезКонтекста
Процедура ОбойтиЭлементыДерева(СтрокиДерева,ТаблицаОтчетовДляВыгрузки)
	Для каждого Элемент Из СтрокиДерева Цикл  
		Если Элемент.Использовать = 1 Тогда
			//Сообщить(элемента.Наименование + " Использовать - " + элемента.Использовать);  
			ЗаполнитьЗначенияСвойств(ТаблицаОтчетовДляВыгрузки.Добавить(),Элемент);
		КонецЕсли;
		ВложенныеСтроки = Элемент.Строки;
		Если ВложенныеСтроки <> Неопределено Тогда
			ОбойтиЭлементыДерева(ВложенныеСтроки,ТаблицаОтчетовДляВыгрузки);
		КонецЕсли
	КонецЦикла
КонецПроцедуры  
&НаСервере
Функция ОтобратьДляВыгрузкиНаСервере()   
	МассивОтчетов = Новый Массив;
	
	ТаблицаОтчетовДляВыгрузки = ТаблицаОтбора();
	ДеревоЗнач = ДанныеФормыВЗначение(ВидыОтчетов,Тип("ДеревоЗначений"));      	
	ОбойтиЭлементыДерева(ДеревоЗнач.Строки,ТаблицаОтчетовДляВыгрузки);
			
	Запрос = Новый Запрос;  
	Запрос.УстановитьПараметр("ВТ_ОтобранныеОтчеты",ТаблицаОтчетовДляВыгрузки);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВТ_ОтобранныеОтчеты.Наименование КАК Наименование,
		|	ВТ_ОтобранныеОтчеты.Использовать КАК Использовать
		|ПОМЕСТИТЬ ВТ_ОтобранныеОтчеты
		|ИЗ
		|	&ВТ_ОтобранныеОтчеты КАК ВТ_ОтобранныеОтчеты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НастраиваемыйОтчет.ВидОтчета.Наименование КАК ВидОтчетаНаименование,
		|	НастраиваемыйОтчет.Ссылка КАК Ссылка
		|ИЗ
		|	ВТ_ОтобранныеОтчеты КАК ВТ_ОтобранныеОтчеты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НастраиваемыйОтчет КАК НастраиваемыйОтчет
		|		ПО ((ВЫРАЗИТЬ(ВТ_ОтобранныеОтчеты.Наименование КАК СТРОКА(120))) = НастраиваемыйОтчет.ВидОтчета.Наименование)";
	
	РезультатЗапроса = Запрос.Выполнить();	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл  
		СтруктураОтчетов = Новый Структура;
		СтруктураОтчетов.Вставить("Ссылка",ВыборкаДетальныеЗаписи.Ссылка);  
		СтруктураОтчетов.Вставить("Наименование",ВыборкаДетальныеЗаписи.ВидОтчетаНаименование); 
		МассивОтчетов.Добавить(СтруктураОтчетов);
	КонецЦикла;	   
	Возврат МассивОтчетов;
КонецФункции  
&НаСервереБезКонтекста
Функция ТаблицаОтбора()   
	ОписаниеТипаСтрока = ОбщегоНазначения.ОписаниеТипаСтрока(120);
	ТабОтобора = Новый ТаблицаЗначений;
	ТабОтобора.Колонки.Добавить("Наименование",ОписаниеТипаСтрока);
	ТабОтобора.Колонки.Добавить("Использовать",ОписаниеТипаСтрока);   
	Возврат ТабОтобора;
КонецФункции
#КонецОбласти

